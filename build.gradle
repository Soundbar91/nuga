plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = project.findProperty('projectGroup') ?: 'com.soundbar91'
version = project.findProperty('projectVersion') ?: '0.0.1-SNAPSHOT'
description = 'multi module architecture study'

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'

	group = rootProject.findProperty('projectGroup') ?: 'com.soundbar91'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom 'org.springframework.boot:spring-boot-dependencies:4.0.1'
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	// Jar manifest configuration for library modules
	tasks.withType(Jar).configureEach {
		manifest {
			attributes(
				'Implementation-Title': project.name,
				'Implementation-Version': project.version,
				'Implementation-Vendor': 'soundbar91'
			)
		}
	}
}
